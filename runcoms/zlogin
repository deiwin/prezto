#
# Executes commands at login post-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Execute code that does not affect the current session in the background.
{
  # Compile the completion dump to increase startup speed.
  zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
  if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
    zcompile "$zcompdump"
  fi
} &!

# Execute code only if STDERR is bound to a TTY.
[[ -o INTERACTIVE && -t 2 ]] && {

  # Print a random, hopefully interesting, adage.
  if (( $+commands[fortune] )); then
    command fortune -c discworld archlinux arresteddevelopment betteroffted vimtips |
    fmt -80 -s                                                                      |
    $(shuf -n 1 -e cowsay cowthink)                                                 \
    -$(shuf -n 1 -e b d g p s t w y)                                                \
    -f $(shuf -n 1 -e $(cowsay -l | tail -n +2))                                    \
    -n
    print
  fi

} >&2
